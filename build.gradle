plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: "scala"
apply plugin: "maven-publish"
sourceCompatibility = 1.7
version = "0.9.0-SNAPSHOT"

def bintrayVersion = {
    if (version.endsWith('-SNAPSHOT')) {
        java.text.SimpleDateFormat format = new java.text.SimpleDateFormat('yyMMddHHmm')
        format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
        return version.replace('SNAPSHOT', format.format(new Date()))
    } else {
        return version
    }
}()

task wrapper(type: Wrapper) {
    gradleVersion = "2.11"
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/nilskp/maven" 
    }
}

jar {
    baseName = "mongolia_2.11"
    manifest {
        attributes "Implementation-Title": "mongolia", "Implementation-Version": version
    }
}

task sourceJar(type: Jar) {
    baseName "mongolia_2.11"
    classifier "sources"
    from sourceSets.main.allScala
}
task docsJar(type: Jar, dependsOn: scaladoc) {
    baseName "mongolia_2.11"
    classifier "docs"
    from scaladoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'nilskp'
            artifactId jar.baseName
            version bintrayVersion

            from components.java
            artifact sourceJar
            artifact docsJar
            pom.withXml { xml ->
                xml.asNode().dependencies.dependency.findAll{ ! ['scuff'].contains(it.artifactId.text()) }.each { dep ->
                    dep.scope[0].value = "provided"
                }
            }
        }
    }
}

bintray {
    user = System.properties.bintrayUser ?: "n/a"
    key = System.properties.bintrayApiKey ?: "n/a"
    dryRun = (user == "n/a" || key == "n/a")
    publish = !dryRun
    publications = ['maven']
    pkg {
        repo = "maven"
        name = "Mongolia"
        version {
            name = bintrayVersion
        }
    }
}

dependencies {
    testCompile "junit:junit:4.11"

    compile "org.scala-lang:scala-library:2.11.7"
    compile "nilskp:scuff_2.11:0.1.5-b1602191540"
    compile "org.mongodb:mongo-java-driver:2.14.1"
}

compileScala {
    scalaCompileOptions.additionalParameters = [
        "-feature", "-optimise","–unchecked", "-deprecation","–explaintypes",
        "–Xdisable-assertions", //"-Xfatal-warnings",
        "-Ybackend:GenBCode", "-Yclosure-elim",
    ]
}

compileTestScala {
    scalaCompileOptions.additionalParameters = [
        "-nowarn",
        "-Xcheckinit", "-optimise",
        "-Ybackend:GenBCode", "-Yclosure-elim",
    ]
}
